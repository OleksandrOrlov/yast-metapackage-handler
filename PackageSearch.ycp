{
	textdomain "PackageSearch";

	import "SearchClient";
	import "Popup";

    integer width = 110;
    integer height = 20;
	string baseURL = "http://benjiweber.co.uk:8080/searchservice/SearchService/";
	string searchURL = baseURL + "Search/Simple/openSUSE_102/";
	list args = WFM::Args();
	if (size(args) > 0)
	{
		searchURL = args[0]:searchURL;
	}

	list<string> items = [];
    term searchBox = `HBox(
	`HSpacing(1),
	`VBox(
	    `VSpacing(0.2),
	    `Heading("Package Search"),
	    `VSpacing(0.2),
	    `VBox(
			
			`HBox(
			`TextEntry(`id( `searchField ), "Enter the search term:" ),
			`VBox(
				`VSpacing(1),
				`PushButton(`id( `searchBtn ),`opt(`default), "Search")
				)
			),
		`VSpacing(0.2),
		`HSpacing(width),
		`HBox(
		    `VSpacing(height),
			`Table(`id(`multi), `opt(`notify),`header( "Name","Version", "Catalogue"),[`item(`id(0),"No Results")])
			//,"Install Now?" ), [`item(`id(0),"No Results\ntest\n\ntesttest","","","")] )
			)
	    ),
	    `VSpacing(0.5),
		`HBox( 
				`PushButton(  `id( `closeBtn ), "Close" ) ,
				`HStretch(), 
				`PushButton(  `id( `configureBtn ), "Configure search providers" ) ,
				`HStretch(), 
				`PushButton(  `id( `installBtn ), "Install selected" )
			),
		`VSpacing(0.5)
	    ),
	`HSpacing(1)
    );
    UI::OpenDialog(searchBox);
	UI::SetFocus(`searchField);
	string ympURL = "";
	symbol input = nil;
	boolean close = false;
	repeat
	{ 
		input = (symbol) UI::UserInput();
		if (input == `searchBtn)
		{
			string searchTerm = (string) UI::QueryWidget(`id(`searchField), `Value);
			list<map<string,list<any> > > packages = SearchClient::Search(searchURL,searchTerm);
			list<any> multiSelectItems = [];
			integer index = 0;
			foreach (map<string,list<any> > package, packages, 
			{
				string name = ((list<string>)package["name"]:[])[0]:"Unnamed";
				string version = ((list<string>)package["version"]:[])[0]:"Unnamed";
				string url = ((list<string>)package["repoURL"]:[])[0]:"Unnamed";
				string checksum = ((list<string>)package["checksum"]:[])[0]:("" + index);
				multiSelectItems = add (multiSelectItems,`item(`id(checksum), name,version,url));//,"No"));
				index = index + 1;
			});
			UI::ChangeWidget(`multi, `Items, multiSelectItems );	
		} else if (input == `installBtn)
		{
			string current_item  = (string) UI::QueryWidget(`id(`multi), `CurrentItem);
			ympURL = baseURL + "YMPs/openSUSE_102/" + current_item;
			y2milestone(ympURL);
			close = true;
		} else if (input == `closeBtn)
		{
			UI::CloseDialog();
			close = true;
		}  else if (input == `configureBtn)
		{
			Popup::Notify("Not implemented yet");
		} else
		{
			/*
			string current_item_no  = (string) UI::QueryWidget(`id(`multi), `CurrentItem);
			term current_item = (term) UI::QueryWidget(`multi, `Item( current_item_no ) );
			string toInstall = current_item[4]:"Yes";
			if (toInstall == "Yes")
				UI::ChangeWidget(`id(`multi), `Item( current_item_no,3), "No" );
			else if (toInstall == "No")
				UI::ChangeWidget(`id(`multi), `Item( current_item_no,3), "Yes" );
			*/
		}
	} until (close);

	if (ympURL != "")
	{
		UI::CloseDialog();
		WFM::call("MetaPackageHandler",[ympURL]);
	}

}
